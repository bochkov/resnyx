plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

group = 'resnyx'
version = '8.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name "reposiliteRepositoryReleases"
        url "https://maven.sergeybochkov.com/releases"
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation platform('com.fasterxml.jackson:jackson-bom:2.18.1')
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
    testImplementation 'ch.qos.logback:logback-classic:1.5.12'
    testImplementation platform('com.konghq:unirest-java-bom:4.4.5')
    testImplementation 'com.konghq:unirest-java-core'
    testImplementation 'com.konghq:unirest-modules-jackson'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            url = "https://maven.sergeybochkov.com/releases"
            credentials {
                username = "$reposilite_user"
                password = "$reposilite_password"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

def isNonStable = { String version ->
    return ['.*-alpha\\d+'].any { it -> version.toLowerCase().matches(it) }
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}